{
  pkgs,
  lib,
  config,
  ...
}:
{
  system.stateVersion = "25.11";

  nix.settings.experimental-features = [
    "nix-command"
    "flakes"
  ];

  # This was generated by nixos-generate-config, and then modified.
  # 1. Removed reference to https://github.com/NixOS/nixpkgs/blob/master/nixos/modules/installer/scan/not-detected.nix
  #    and place its content `hardware.enableRedistributableFirmware = true;` inline.
  # 2. Set updateMicrocode to true
  # 3. Use /dev/disk/by-label instead of /dev/disk/by-uuid
  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
  boot.initrd.availableKernelModules = [
    "xhci_pci" # USB 3.0 controller driver
    "nvme" # NVMe SSD driver
    "ahci" # AHCI driver
    "usb_storage" # USB storage driver
    "usbhid" # USB Human Interface Devices driver for keyboard
    "sd_mod" # SCSI driver
  ];

  # Taken from https://nixos.wiki/wiki/Linux_kernel#Out-of-tree_kernel_modules
  boot.extraModulePackages = [
    (config.boot.kernelPackages.callPackage (
      {
        stdenv,
        fetchFromGitHub,
        kernel,
      }:
      stdenv.mkDerivation {
        pname = "led-ugreen";
        version = "0.3";
        src = fetchFromGitHub {
          owner = "miskcoo";
          repo = "ugreen_leds_controller";
          rev = "v0.3";
          hash = "sha256-IY8b9Hc2t/Zcz4TlQ5p+D/Q/ilE9S308DIZFuJh/isE=";
        };
        sourceRoot = "source/kmod";
        hardeningDisable = [
          "pic"
          "format"
        ];
        nativeBuildInputs = kernel.moduleBuildDependencies;
        makeFlags = [
          "KERNELRELEASE=${kernel.modDirVersion}"
          "KDIR=${kernel.dev}/lib/modules/${kernel.modDirVersion}/build"
          "INSTALL_MOD_PATH=$(out)"
        ];
      }
    ) { })
  ];
  boot.kernelModules = [
    "kvm-intel"

    # https://github.com/miskcoo/ugreen_leds_controller/tree/v0.3?tab=readme-ov-file#the-kernel-module
    # led-ugreen requires i2c-dev to work
    # led-ugreen is built above.
    "i2c-dev"
    "led-ugreen"
  ];
  # According to https://github.com/miskcoo/ugreen_leds_controller/tree/v0.3?tab=readme-ov-file#the-kernel-module
  # It is required to register the device on every boot.
  # This service is taken from https://github.com/miskcoo/ugreen_leds_controller/blob/v0.3/scripts/systemd/ugreen-probe-leds.service
  # and the script https://github.com/miskcoo/ugreen_leds_controller/blob/v0.3/scripts/ugreen-probe-leds
  # and then simplified into a oneliner.
  # I have tried different approaches but it is very stable on every reboot.
  # Sometimes, after reboot /sys/class/leds do not contain the expected files.
  #
  # systemd.services.ugreen-probe-leds = {
  #   wantedBy = [ "multi-user.target" ];
  #   # For some unknown reason, after systemd-modules-load.service does not work.
  #   after = [ "multi-user.target" ];
  #   requires = [ "systemd-modules-load.service" ];
  #   preStart = ''
  #     sleep 5
  #   '';
  #   script = ''
  #     echo "led-ugreen 0x3a" > /sys/bus/i2c/devices/i2c-0/new_device
  #   '';
  #   serviceConfig = {
  #     Type = "oneshot";
  #     RemainAfterExit = true;
  #     StandardOutput = "journal";
  #   };
  # };

  hardware.enableRedistributableFirmware = true;
  hardware.cpu.intel.updateMicrocode = true;

  # ZFS
  # Enable ZFS
  boot.supportedFilesystems = [ "zfs" ];
  # The doc says it should be set to false.
  # https://search.nixos.org/options?channel=25.11&query=boot.zfs&show=boot.zfs.forceImportRoot
  boot.zfs.forceImportRoot = false;
  # The doc says ZFS requires this.
  # https://search.nixos.org/options?channel=25.11&query=ZFS&show=networking.hostId
  # This was generated with `head -c 8 /etc/machine-id`
  networking.hostId = "0c1b9b5d";
  services.zfs.autoScrub.enable = true;
  services.zfs.autoScrub.interval = "weekly";

  swapDevices = [
    {
      device = "/dev/disk/by-label/swap";
    }
  ];
  fileSystems."/" = {
    device = "/dev/disk/by-label/nixos";
    fsType = "ext4";
  };
  fileSystems."/boot" = {
    device = "/dev/disk/by-label/boot";
    fsType = "vfat";
    options = [
      "fmask=0077"
      "dmask=0077"
    ];
  };
  fileSystems."/data" = {
    # This has to be a pool name, not an absolute path.
    device = "data";
    fsType = "zfs";
  };

  services.samba.enable = true;
  services.samba.openFirewall = true;
  services.samba.settings.global.security = "user";
  services.samba.settings.nas_samba = {
    path = "/data";
    browsable = "yes";
    public = "no";
    writeable = "yes";
    "valid users" = "nixos";
    "create mask" = "0666";
    "directory mask" = "0777";
  };

  # sops by default use SSH key to decrypt if services.openssh.enable = true.
  # So we only need to run `sops updatekeys path/to/secret` on a machine that can decrypt the file.
  sops.defaultSopsFile = ./secrets/secrets.yaml;
  sops.defaultSopsFormat = "yaml";
  sops.secrets."rclone/remotes/googledrive/client_id" = { };
  sops.secrets."rclone/remotes/googledrive/client_secret" = { };
  sops.secrets."rclone/remotes/googledrive/token" = { };
  sops.secrets."nas_samba/password" = { };
  sops.templates."rclone.conf".content = ''
    [googledrive]
    type = drive
    client_id = ${config.sops.placeholder."rclone/remotes/googledrive/client_id"}
    client_secret = ${config.sops.placeholder."rclone/remotes/googledrive/client_secret"}
    scope = drive
    token = ${config.sops.placeholder."rclone/remotes/googledrive/token"}
    team_drive =
  '';
  services.restic.backups.data = {
    rcloneConfigFile = "/run/secrets/rendered/rclone.conf";
    repository = "rclone:googledrive:restic";
    passwordFile = "/run/secrets/nas_samba/password";
    initialize = true;
    paths = [
      "/data"
    ];
    pruneOpts = [
      "--keep-within 2y"
      "--keep-within-hourly 24h"
      "--keep-within-daily 7d"
      "--keep-within-weekly 28d"
      "--keep-within-monthly 3m"
      "--keep-within-yearly 1y"
    ];
    timerConfig = {
      Persistent = true;
      OnCalendar = "hourly";
      RandomizedDelaySec = 300;
    };
  };

  services.syncthing.enable = true;
  services.syncthing.user = "nixos";
  services.syncthing.group = "users";
  services.syncthing.dataDir = "/home/nixos/syncthing";
  services.syncthing.overrideDevices = true;
  services.syncthing.overrideFolders = true;
  services.syncthing.openDefaultPorts = true;
  services.syncthing.settings.devices."LouisChan16" = {
    id = "DZH4SZM-YXPC5JN-3KCTXKQ-MRCJ5QM-STPPXNN-ZY55RQG-YRIUWHV-NBXKNQ5";
    addresses = [
      "tcp://louischan-16-pro.tail78d407.ts.net:22000"
    ];
  };
  services.syncthing.settings.devices."louischan-m4" = {
    id = "BA72RDZ-AWAG46I-KWWRSPX-XHQC5OH-J2ZMHBU-LDUMBQU-TWUDZ6M-4XGGUQJ";
    addresses = [
      "tcp://louischan-m4.tail78d407.ts.net:22000"
    ];
  };
  services.syncthing.settings.folders."/data/louischan/obsidian/vaults/personal/" = {
    id = "obsidian-personal";
    type = "sendreceive";
    devices = [
      "LouisChan16"
      "louischan-m4"
    ];
  };
  services.syncthing.settings.options = {
    listenAddresses = [ "tcp://0.0.0.0:22000" ];

    globalAnnounceServers = [ ];
    stunServers = [ ];
    urURL = "https://0.0.0.0";
    releasesURL = "https://0.0.0.0";
    crURL = "https://0.0.0.0";

    globalAnnounceEnabled = false;
    localAnnounceEnabled = false;
    crashReportingEnabled = false;
    relaysEnabled = false;
    natEnabled = false;
    announceLANAddresses = false;
    startBrowser = false;
    urAccepted = -1;

    autoUpgradeIntervalH = 0;
  };

  boot.loader.systemd-boot.enable = true;
  boot.loader.efi.canTouchEfiVariables = true;
  time.timeZone = "Asia/Hong_Kong";
  i18n.defaultLocale = "en_US.UTF-8";

  networking.hostName = "nas";
  networking.networkmanager.enable = true;
  networking.firewall.enable = true;

  services.openssh.enable = true;
  services.openssh.settings = {
    # Integration with vivid on the connecting client.
    AcceptEnv = "LS_COLORS";
  };

  services.tailscale.enable = true;

  security.sudo.wheelNeedsPassword = false;

  users.users.nixos = {
    isNormalUser = true;
    extraGroups = [
      "wheel"
      "networkmanager"
    ];
    openssh.authorizedKeys.keys = [
      "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAID10oX/o5t2FBc/DYIQMLqpmUYzFMiGebCtYCG0B1ik+ openpgp:0x5B63D933"
    ];
  };

  programs.git.enable = true;
  programs.vim.enable = true;
  environment.systemPackages = with pkgs; [
    sops
    restic
    syncthing
  ];

  # Usage:
  #   nh os switch
  programs.nh.enable = true;
  programs.nh.flake = "/home/nixos/dotfiles";
}
